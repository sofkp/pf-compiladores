Program ::= (StructDec | GlobalVarDec)* FunDec*
GlobalVarDec ::= 'static' 'mut'? id (':' Type)? '=' CExp ';'
StructDec ::= 'struct' id '{' StructFieldList '}'
StructFieldList ::= StructField (',' StructField)* | ε
StructField ::= id ':' Type
Type ::= 'i32' | 'i64' | 'bool' | id | ArrayType
ArrayType ::= '[' Type ';' NUM ']'
FunDec ::= 'fn' id '(' ParamList? ')' ReturnType? Body
ReturnType ::= '->' Type
ParamList ::= Param (',' Param)*
Param ::= id ':' Type
Body ::= '{' LocalVarDec* Stm* '}'
LocalVarDec ::= 'let' 'mut'? id (':' Type)? ('=' CExp)? ';'
Stm ::= AssignStm
    | CallStm
    | PrintStm
    | ReturnStm
    | IfStm
    | WhileStm
    | ForStm
    | BlockStm
AssignStm ::= LHS '=' CExp ';'
LHS ::= id
        | LHS '[' CExp ']'
        | LHS '.' id
CallStm ::= id '(' ArgList? ')' ';'
ArgList ::= CExp (',' CExp)*
PrintStm ::= 'println!' '(' CExp (',' CExp)* ')' ';'
ReturnStm ::= 'return' CExp ';'
IfStm ::= 'if' CExp Body ('else' Body)?
WhileStm ::= 'while' CExp Body
ForStm ::= 'for' id 'in' CExp RangeOp CExp Body
RangeOp ::= '..' | '..='
BlockStm ::= Body
CExp ::= Exp (('<' | '<=' | '==' | '>' | '>=') Exp)?
Exp ::= Term (('+' | '-') Term)∗
Term ::= Factor (('*' | '/') Factor)∗
Factor ::= Primary PostfixPart*
Primary ::= id | Num | Bool | String | '(' CExp ')' | ArrayLit | StructLit
PostfixPart ::= '[' CExp ']' | '.' id | '(' ArgList? ')'
ArrayLit ::= '[' (CExp (',' CExp)*)? ']'
StructLit ::= id '{' StructLitFields '}'
StructLitFields ::= StructLitField (',' StructLitField)* | ε
StructLitField ::= id ':' CExp

Program ::= GlobalVarDecList  FunDecList
GlobalVarDecList ::= (GlobalVar | StructDec)*
FunDecList ::= (FunDec)+ 
GlobalVar ::= 'static' ('mut')? id ':' Type '=' CExp ';' | 'static' ('mut')? id '=' CExp ';'
StructDec ::= 'struct' id '{' StructFieldList '}'
StructFieldList ::= StructField (',' StructField )* | ε
StructField ::= id ':' Type
Type ::= 'i32' | 'i64' | 'bool' | ArrayType | StructType
ArrayType ::= '[' Type ';' Num ']'
StructType ::= id
FunDec ::= 'fn' id '(' (ParamDecList)? ')' ('->' Type)?  Body
ParamDecList ::= ParamDec (',' ParamDec)*
ParamDec ::= Type id
Body ::= '{' LocalVarDecList StmList '}'
LocalVarDecList ::= LocalVar (LocalVar)* | ε
LocalVar ::= 'let' ('mut')? id ':' Type ('=' CExp)? ';' | 'let' ('mut')? id '=' CExp ';'
StmList  ::= Stm (';' Stm)*
Stm ::= id '=' CExp  
      | id '(' (ArgList)? ')'  
      | 'println!' '(' CExp ')' 
      | 'return' CExp
      | 'if' CExp Body ('else' Body)?
      | 'while' CExp Body
      | 'for' id 'in' CExp ('..' | '..=') CExp Body
      | Cexp '.' id '=' Cexp //struct assign
        Cexp '[' Cexp ']' '=' Cexp //array assign
CExp ::= Exp (('<' | '<=' | '==' | '>' | '>=') Exp)?
Exp ::= Term (('+' | '-') Term)∗
Term ::= Factor (('*' | '/') Factor)∗
Factor ::= id | Num | Bool | '('Exp')' | id '(' (ArgList)? ')' | 
        Array | Struct | String | IndexedAccess | FieldAccess
ArgList ::= CExp (',' CExp)∗
Bool ::= 'true' | 'false'
String ::= '"' id '"'
Array ::= '[' (CExp (',' CExp)*)? ']'
IndexedAccess ::= CExp '[' CExp ']'
Struct ::= id '{' StructLitFields '}'
StructLitFields ::= StructLitField (',' StructLitField)* | ε
StructLitField ::= id ':' CExp
FieldAccess ::= CExp '.' id